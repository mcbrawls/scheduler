plugins {
    id "org.jetbrains.kotlin.jvm"
    id "application"
    id "maven-publish"
}

def ENV = System.getenv()

group = maven_group
version = project_version

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test"
}

test {
    useJUnitPlatform()
}

kotlin {
    jvmToolchain(17)
}

application {
    mainClassName = "MainKt"
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        // Andante's Maven
        def mavenUsername = ENV["MAVEN_USERNAME_ANDANTE"]
        def mavenPassword = ENV["MAVEN_PASSWORD_ANDANTE"]
        if (mavenUsername && mavenPassword) {
            maven {
                name = "Andante"
                url  = "https://maven.andante.dev/releases/"
                credentials.username = mavenUsername
                credentials.password = mavenPassword
            }
        }
    }
}
